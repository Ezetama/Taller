{Bienvenidos al entorno CMRE.
Lo siguiente es un código de ejemplo que implementa un
proceso que recibe un número de avenida como parámetro,
se posiciona en esa avenida y la recorre.}

programa ejemplo
procesos
  proceso juntarFlores (ES cantF: numero)
  comenzar
    mientras HayFlorEnLaEsquina
      tomarFlor
      cantF:=cantF+1
  fin
  proceso dejarFlores (E cantF: numero; E miCalle: numero; E miAvenida: numero)
  comenzar  
    repetir cantF
      BloquearEsquina(50,50)
      Pos (50,50)
      depositarFlor
      Pos (miAvenida,miCalle)
      LiberarEsquina (50,50)
  fin
  
  proceso Identificar
  comenzar
    EnviarMensaje (1,r1)
    EnviarMensaje (2,r2)
    EnviarMensaje (3,r3)
  fin
  proceso hacerCuadrado (E id: numero; ES cantF: numero)
  variables
    lado: numero
  comenzar
    EnviarMensaje (id,jefecito)
    RecibirMensaje(lado,jefecito)
    repetir 4
      repetir lado
        juntarFlores(cantF)
        mover
      derecha
    juntarFlores(cantF)
    repetir (lado+1)
      mover
  fin
  proceso enviarLados 
  variables
    terminado: numero
    id: numero
    lado: numero
  comenzar
    terminado:=0
    mientras terminado<>3
      Random (lado,1,5)
      RecibirMensaje (id,*)
      si id=4
        terminado:= terminado+1
      sino
        si id=1
          EnviarMensaje (lado,r1)
        sino
          si id=2
            EnviarMensaje (lado,r2)
          sino
            EnviarMensaje (lado,r3)  
  fin
  proceso juntarEInformar
  variables
    cant: numero
  comenzar
    cant:=0
    Pos (50,50)
    mientras HayFlorEnLaEsquina
      tomarFlor
      cant:=cant+1
    Informar (cant)
  fin
areas
  area1: AreaP (1,1,7,40)
  area2: AreaP (8,1,14,40)
  area3: AreaP (15,1,21,40)
  deposito: AreaC (50,50,50,50)
  areaJefe: AreaP(22,1,22,1)
robots
  robot jefe
  variables
    ok: boolean
    x: numero
  comenzar
    x:=66
    Identificar
    enviarLados
    Informar (x)
    juntarEInformar
  fin
  robot robot1
  variables
    id: numero
    cantF: numero
    miCalle: numero
    miAvenida: numero
  comenzar
    RecibirMensaje (id,jefecito)
    repetir 3
      hacerCuadrado (id,cantF)
    miCalle:=PosCa
    miAvenida:= PosAv
    dejarFlores (cantF,miCalle,miAvenida)
    EnviarMensaje (4,jefecito)
  fin
variables
  r1: robot1
  r2: robot1
  r3: robot1
  jefecito: jefe
comenzar
  AsignarArea(r1, area1)
  AsignarArea(r1, deposito)
  AsignarArea(r2, area2)
  AsignarArea(r2, deposito)
  AsignarArea(r3, area3)
  AsignarArea(r3, deposito)
  AsignarArea(jefecito, areaJefe)
  AsignarArea(jefecito, deposito)
  Iniciar(r1, 1,1)
  Iniciar(r2, 8,1)
  Iniciar(r3, 15,1)
  Iniciar(jefecito, 22,1)
fin